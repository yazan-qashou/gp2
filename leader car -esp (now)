#include "BluetoothSerial.h"
#include <WiFi.h>
#include <esp_now.h>

BluetoothSerial serialBT;
char btSignal;
int Speed = 100;
int enA = 5, enB = 23;
int IN1 = 22, IN2 = 21, IN3 = 19, IN4 = 18;
#define R 0
#define L 1

uint8_t followerAddr[] = {0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF};

// Leader Data Structure
typedef struct {
  int posX;
  int posY;
  char command;
  int speed;
  char direction;
} LeaderData;

LeaderData leaderData = {100, 100, 'S', 0, 'N'}; // Initialize with a non-zero position

void setup() {
  Serial.begin(115200);

  serialBT.begin("car(yazan)");
  Serial.println("Bluetooth initialized");

  pinMode(enA, OUTPUT);
  pinMode(enB, OUTPUT);
  ledcSetup(R, 5000, 8);
  ledcAttachPin(enA, R);
  ledcSetup(L, 5000, 8);
  ledcAttachPin(enB, L);
  pinMode(IN1, OUTPUT);
  pinMode(IN2, OUTPUT);
  pinMode(IN3, OUTPUT);
  pinMode(IN4, OUTPUT);
  stop();

  WiFi.mode(WIFI_STA);
  if (esp_now_init() != ESP_OK) {
    Serial.println("Error initializing ESP-NOW");
    return;
  }
  Serial.println("ESP-NOW initialized");

  esp_now_peer_info_t peerInfo = {};
  memcpy(peerInfo.peer_addr, followerAddr, 6);
  peerInfo.channel = 0;
  peerInfo.encrypt = false;

  if (esp_now_add_peer(&peerInfo) != ESP_OK) {
    Serial.println("Failed to add peer");
    return;
  }
}

void loop() {
  while (serialBT.available()) {
    btSignal = serialBT.read();

    if (btSignal == '0') Speed = 100;
    else if (btSignal == '1') Speed = 110;
    else if (btSignal == '2') Speed = 120;
    else if (btSignal == '3') Speed = 130;
    else if (btSignal == '4') Speed = 140;
    else if (btSignal == '5') Speed = 150;
    else if (btSignal == '6') Speed = 180;
    else if (btSignal == '7') Speed = 200;
    else if (btSignal == '8') Speed = 220;
    else if (btSignal == '9') Speed = 240;
    else if (btSignal == 'q') Speed = 255;

    if (btSignal == 'F') { forward(); leaderData.direction = 'N'; }
    else if (btSignal == 'B') { backward(); leaderData.direction = 'S'; }
    else if (btSignal == 'L') { left(); leaderData.direction = 'W'; }
    else if (btSignal == 'R') { right(); leaderData.direction = 'E'; }
    else if (btSignal == 'S') stop();

    sendLeaderData(btSignal, Speed, leaderData.direction);
  }
}

void forward() {
  ledcWrite(R, Speed);
  ledcWrite(L, Speed);
  digitalWrite(IN1, HIGH); digitalWrite(IN2, LOW);
  digitalWrite(IN3, LOW); digitalWrite(IN4, HIGH);
}

void backward() {
  ledcWrite(R, Speed);
  ledcWrite(L, Speed);
  digitalWrite(IN1, LOW); digitalWrite(IN2, HIGH);
  digitalWrite(IN3, HIGH); digitalWrite(IN4, LOW);
}

void left() {
  ledcWrite(R, Speed);
  ledcWrite(L, Speed);
  digitalWrite(IN1, HIGH); digitalWrite(IN2, LOW);
  digitalWrite(IN3, HIGH); digitalWrite(IN4, LOW);
}

void right() {
  ledcWrite(R, Speed);
  ledcWrite(L, Speed);
  digitalWrite(IN1, LOW); digitalWrite(IN2, HIGH);
  digitalWrite(IN3, LOW); digitalWrite(IN4, HIGH);
}

void stop() {
  ledcWrite(R, 0);
  ledcWrite(L, 0);
  digitalWrite(IN1, LOW); digitalWrite(IN2, LOW);
  digitalWrite(IN3, LOW); digitalWrite(IN4, LOW);
}

void sendLeaderData(char command, int speed, char direction) {
  leaderData.command = command;
  leaderData.speed = speed;

  if (command == 'F') leaderData.posX += 10;
  else if (command == 'B') leaderData.posX -= 10;
  else if (command == 'L') leaderData.posY -= 10;
  else if (command == 'R') leaderData.posY += 10;

  esp_now_send(followerAddr, (uint8_t*)&leaderData, sizeof(leaderData));
}
